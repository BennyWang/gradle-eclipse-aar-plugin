buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

plugins {
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.2'
    id 'net.saliman.cobertura' version '2.2.5'
    id 'com.github.kt3k.coveralls' version '2.3.1'
    id 'com.github.ksoichiro.build.info' version '0.1.2'
}

apply plugin: 'groovy'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'signing'

group = GROUP
version = VERSION_NAME
archivesBaseName = POM_ARTIFACT_ID

// To release:
// ./gradlew clean assemble uploadArchives -Prelease
def isReleaseBuild
if (hasProperty("release")) {
    isReleaseBuild = true
}

def sonatypeRepositoryUrl

if (isReleaseBuild) {
    if ("$version".endsWith('-SNAPSHOT')) {
        println "SNAPSHOT BUILD $version"
        sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
    } else {
        println "RELEASE BUILD $version"
        sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    }
} else {
    println "DEBUG BUILD $version"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
}

// Note: These properties must be defined in ~/.gradle/gradle.properties
// and signing.keyId, signing.password, signing.secretKeyRingFile, too.
def username = hasProperty("nexusUsername") ? project.getProperty("nexusUsername") : ""
def password = hasProperty("nexusPassword") ? project.getProperty("nexusPassword") : ""

def bintrayUsername = hasProperty("bintrayUsername") ? project.getProperty("bintrayUsername") : ""
def bintrayApiKey = hasProperty("bintrayApiKey") ? project.getProperty("bintrayApiKey") : ""

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.apache.maven:maven-artifact:3.3.9'
    testCompile 'com.android.tools.build:gradle:1.0.0'
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.3') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

repositories {
    mavenCentral()
}

install {
    repositories.mavenInstaller {
        pom.artifactId = POM_ARTIFACT_ID
    }
}

cobertura {
    coverageFormats = ['html', 'xml']
    coverageSourceDirs = sourceSets.main.groovy.srcDirs
}

afterEvaluate { project ->
    uploadArchives {
        if (isReleaseBuild) {
            repositories {
                mavenDeployer {
                    beforeDeployment { deployment -> signing.signPom(deployment) }

                    repository(url: sonatypeRepositoryUrl) {
                        authentication(userName: username, password: password)
                    }

                    pom.project {
                        name POM_NAME
                        groupId GROUP
                        description POM_DESCRIPTION
                        url POM_URL
                        inceptionYear POM_INCEPTION_YEAR

                        scm {
                            url POM_SCM_URL
                            connection POM_SCM_CONNECTION
                            developerConnection POM_SCM_DEV_CONNECTION
                        }

                        licenses {
                            license {
                                name POM_LICENSE_NAME
                                url POM_LICENSE_URL
                                distribution POM_LICENSE_DIST
                            }
                        }

                        developers {
                            developer {
                                id POM_DEVELOPER_ID
                                name POM_DEVELOPER_NAME
                                url POM_DEVELOPER_URL
                            }
                        }
                    }
                }
            }
        } else {
            // for development
            repositories {
                mavenDeployer {
                    repository(url: uri('../samples/repo'))
                }
            }
        }
    }

    signing {
        required { isReleaseBuild && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            plugin(MavenPublication) {
                groupId GROUP
                artifactId POM_ARTIFACT_ID
                version VERSION_NAME

                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey

    publications = ['plugin']

    pkg {
        repo = 'maven'
        name = POM_NAME
        desc = POM_DESCRIPTION
        websiteUrl = POM_URL
        issueTrackerUrl = BINTRAY_ISSUE_TRACKER_URL
        vcsUrl = BINTRAY_VCS_URL
        licenses = ['Apache-2.0']
        labels = ['gradle', 'plugin', 'Android', 'eclipse', 'AAR']
        publicDownloadNumbers = true
        version {
            attributes = ['gradle-plugin': 'com.github.ksoichiro.eclipse.aar:com.github.ksoichiro:gradle-eclipse-aar-plugin']
        }
    }
}

pluginBundle {
    website = POM_URL
    vcsUrl = BINTRAY_VCS_URL

    description = POM_DESCRIPTION

    plugins {
        plugin {
            id = GRADLE_PLUGIN_ID
            displayName = POM_NAME
            tags = ['Android', 'eclipse', 'AAR']
        }
    }

    mavenCoordinates {
        artifactId = POM_ARTIFACT_ID
    }
}
